buildscript {
    repositories {
        mavenCentral()
    }

    dependencies {
        classpath 'com.neenbedankt.gradle.plugins:android-apt:1.8'
        classpath 'com.github.ben-manes:gradle-versions-plugin:0.12.0'
    }
}

plugins {
    id "me.tatarka.retrolambda" version "3.2.5"
}

apply plugin: 'com.android.application'
apply plugin: 'com.neenbedankt.android-apt'
apply plugin: 'com.github.ben-manes.versions'

android {
    compileSdkVersion 23
    buildToolsVersion "23.0.3"

    defaultConfig {
        applicationId "lt.vilnius.tvarkau"
        minSdkVersion 15
        targetSdkVersion 23
        versionCode 1
        versionName "3.0"
        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
}

repositories {
    maven { url "https://clojars.org/repo/" }
    maven { url "http://repo.commonsware.com.s3.amazonaws.com" }
    maven { url "https://jitpack.io" }
}

ext {
    junitVersion = '4.12'
    supportLibraryVersion = '23.4.0'
    playServicesVersion = '9.0.2'
    butterKnifeVersion = '8.0.1'
    glideVersion = '3.7.0'
    icepickVersion = '3.2.0'
    retrofitVersion = '2.0.2'
    gsonVersion = '2.6.2'
    daggerVersion = '2.4'
    autoDaggerVersion = '1.1'
    javaxAnnotationVersion = '1.0'
    viewPagerIndicatorVersion = '2.4.1.1@aar'
    tedPickerVersion = 'v1.0.10'
    eventBusVersion = '3.0.0'
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')

    testCompile "junit:junit:$junitVersion"

    compile "com.android.support:support-v4:$supportLibraryVersion"
    compile "com.android.support:appcompat-v7:$supportLibraryVersion"
    compile "com.android.support:recyclerview-v7:$supportLibraryVersion"
    compile "com.android.support:gridlayout-v7:$supportLibraryVersion"
    compile "com.android.support:design:$supportLibraryVersion"

    compile "com.google.android.gms:play-services-maps:$playServicesVersion"
    compile "com.google.android.gms:play-services-location:$playServicesVersion"
    compile "com.google.android.gms:play-services-auth:$playServicesVersion"

    compile "com.google.firebase:firebase-core:$playServicesVersion"
    compile "com.google.firebase:firebase-crash:$playServicesVersion"

    compile "frankiesardo:icepick:$icepickVersion"
    provided "frankiesardo:icepick-processor:$icepickVersion"

    compile "com.jakewharton:butterknife:$butterKnifeVersion"
    apt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    compile "com.github.bumptech.glide:glide:$glideVersion"

    compile "com.squareup.retrofit2:retrofit:$retrofitVersion"
    compile "com.squareup.retrofit2:converter-gson:$retrofitVersion"

    compile "com.google.code.gson:gson:$gsonVersion"

    compile "com.google.dagger:dagger:$daggerVersion"
    apt "com.google.dagger:dagger-compiler:$daggerVersion"

    apt "com.github.lukaspili.autodagger2:autodagger2-compiler:$autoDaggerVersion"
    compile "com.github.lukaspili.autodagger2:autodagger2:$autoDaggerVersion"

    provided "javax.annotation:jsr250-api:$javaxAnnotationVersion"

    compile "fr.avianey.com.viewpagerindicator:library:$viewPagerIndicatorVersion"

    compile "com.github.ParkSangGwon:TedPicker:$tedPickerVersion"

    compile "org.greenrobot:eventbus:$eventBusVersion"
}

// https://github.com/ben-manes/gradle-versions-plugin
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'