plugins {
    id 'com.github.ben-manes.versions' version '0.15.0'
}

apply plugin: 'com.android.application'
apply plugin: 'com.google.firebase.firebase-perf'
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def versionMajor = 4
def versionMinor = 0
def versionPatch = 1
def versionMicro = 0

android {
    compileSdkVersion 26

    defaultConfig {
        applicationId "lt.vilnius.tvarkau"
        minSdkVersion 16
        targetSdkVersion 26
        versionCode versionMajor * 1000000 + versionMinor * 10000 + versionPatch * 100 + versionMicro
        versionName "$versionMajor.$versionMinor.$versionPatch.$versionMicro"

        resConfigs "lt", "en"

        vectorDrawables.useSupportLibrary = true
    }

    buildTypes {
        release {
            minifyEnabled true
            shrinkResources true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
        debug {
            multiDexEnabled = true
        }
    }

    lintOptions {
        abortOnError false
    }

    signingConfigs {
        debug {
            storeFile file("debug.keystore")
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    testOptions {
        unitTests.returnDefaultValues = true
        unitTests.all {
            maxHeapSize '2048m'
            minHeapSize '1600m'
            jvmArgs "-XX:MaxPermSize=1024m"
        }
    }
}

repositories {
    maven { url "https://clojars.org/repo/" }
    maven { url "https://jitpack.io" }
    mavenCentral()
}

ext {
    junitVersion = '4.12'
    supportLibraryVersion = '26.0.0'
    playServicesVersion = '11.0.2'
    butterKnifeVersion = '8.6.0'
    glideVersion = '3.8.0'
    retrofitVersion = '2.3.0'
    gsonVersion = '2.8.1'
    daggerVersion = '2.11'
    javaxAnnotationVersion = '1.0'
    viewPagerIndicatorVersion = '2.4.1.1@aar'
    eventBusVersion = '3.0.0'
    stethoVersion = '1.5.0'
    okhttpVersion = '3.8.1'
    rxAndroidVersion = '1.2.1'
    rxJavaVersion = '1.3.0'
    parcelerVersion = '1.1.9'
    threeTenVersion = '1.0.5'
    photoView = '1.3.1'
    robolectricVersion = '3.3.2'
    kotlinMockitoVersion = '1.5.0'
    leakCanaryVersion = '1.5.1'
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')

    implementation "com.android.support:support-fragment:$supportLibraryVersion"
    implementation "com.android.support:appcompat-v7:$supportLibraryVersion"
    implementation "com.android.support:recyclerview-v7:$supportLibraryVersion"
    implementation "com.android.support:gridlayout-v7:$supportLibraryVersion"
    implementation "com.android.support:design:$supportLibraryVersion"
    implementation "com.android.support:preference-v14:$supportLibraryVersion"

    implementation "com.google.android.gms:play-services-maps:$playServicesVersion"
    implementation "com.google.android.gms:play-services-location:$playServicesVersion"
    implementation "com.google.android.gms:play-services-places:$playServicesVersion"

    implementation "com.google.firebase:firebase-core:$playServicesVersion"
    implementation "com.google.firebase:firebase-crash:$playServicesVersion"
    implementation "com.google.firebase:firebase-perf:$playServicesVersion"

    implementation "com.jakewharton:butterknife:$butterKnifeVersion"
    kapt "com.jakewharton:butterknife-compiler:$butterKnifeVersion"

    implementation "com.github.bumptech.glide:glide:$glideVersion"

    implementation "com.squareup.retrofit2:retrofit:$retrofitVersion"
    implementation "com.squareup.retrofit2:converter-gson:$retrofitVersion"
    implementation "com.squareup.retrofit2:adapter-rxjava:$retrofitVersion"

    implementation "com.google.code.gson:gson:$gsonVersion"

    implementation "com.google.dagger:dagger:$daggerVersion"
    kapt "com.google.dagger:dagger-compiler:$daggerVersion"

    compileOnly "javax.annotation:jsr250-api:$javaxAnnotationVersion"

    implementation "fr.avianey.com.viewpagerindicator:library:$viewPagerIndicatorVersion"

    implementation "org.greenrobot:eventbus:$eventBusVersion"

    implementation "com.facebook.stetho:stetho:$stethoVersion"
    implementation "com.facebook.stetho:stetho-okhttp3:$stethoVersion"

    implementation "com.mixpanel.android:mixpanel-android:5.1.4"

    implementation "com.squareup.okhttp3:okhttp:$okhttpVersion"
    implementation "com.squareup.okhttp3:logging-interceptor:$okhttpVersion"

    implementation "io.reactivex:rxandroid:$rxAndroidVersion"
    implementation "io.reactivex:rxjava:$rxJavaVersion"

    implementation "org.parceler:parceler-api:$parcelerVersion"
    kapt "org.parceler:parceler:$parcelerVersion"

    implementation "com.jakewharton.threetenabp:threetenabp:$threeTenVersion"

    implementation "com.github.chrisbanes:PhotoView:$photoView"

    implementation "org.jetbrains.kotlin:kotlin-stdlib:$kotlinVersion"
    implementation 'com.jakewharton.timber:timber:4.5.1'

    implementation 'com.github.jkwiecien:EasyImage:2.0.2'

    debugImplementation "com.squareup.leakcanary:leakcanary-android:$leakCanaryVersion"
    releaseImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"
    testImplementation "com.squareup.leakcanary:leakcanary-android-no-op:$leakCanaryVersion"

    kaptTest "com.google.dagger:dagger-compiler:$daggerVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "junit:junit:$junitVersion"
    testImplementation "org.robolectric:robolectric:$robolectricVersion"
    testImplementation "org.robolectric:shadows-support-v4:$robolectricVersion"
    testImplementation "org.jetbrains.kotlin:kotlin-test:$kotlinVersion"
    testImplementation "com.google.dagger:dagger:$daggerVersion"
    testImplementation("com.nhaarman:mockito-kotlin:$kotlinMockitoVersion") {
        exclude module: 'kotlin-stdlib'
    }
    testImplementation 'org.easytesting:fest-util:1.2.5'
    testImplementation 'org.easytesting:fest-reflect:1.4.1'
    testImplementation("com.squareup.assertj:assertj-android:1.1.1") {
        exclude group: 'com.android.support'
    }

    // https://github.com/robolectric/robolectric/issues/1932#issuecomment-219796474
    testImplementation 'org.khronos:opengl-api:gl1.1-android-2.1_r1'
}

task versionName() {
    description "Prints version name"

    println android.defaultConfig.versionName
}

// https://github.com/ben-manes/gradle-versions-plugin
dependencyUpdates.resolutionStrategy = {
    componentSelection { rules ->
        rules.all { ComponentSelection selection ->
            boolean rejected = ['alpha', 'beta', 'rc', 'cr', 'm'].any { qualifier ->
                selection.candidate.version ==~ /(?i).*[.-]${qualifier}[.\d-]*/
            }
            if (rejected) {
                selection.reject('Release candidate')
            }
        }
    }
}

apply plugin: 'com.google.gms.google-services'
